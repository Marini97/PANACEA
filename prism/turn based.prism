// Turn-based stochastic multi-player games
smg

// attacker and defender actors
player attacker
	attacker, 
	[bribe], [exfiltrateData], [bufferOverflow], [getLoginData], [searchOffice], [pathTraversal], [getFiles], [webRecon]
endplayer
player defender
	defender,
	[changeCredentials], [deactivateDOCKS5Proxy], [protectFiles], [disableCGIscripts], [reconfigureApache],	[checkMySQLUpdates], [backupMySQL],
	[installMySQL], 	[rebootMySQL], [updateMySQL], [checkApacheUpdates], [backupApache], [installApache], [rebootApache], [updateApache]
endplayer

global sched : [1..2] init 1;

// global attributes accessible from both players
global AccesstoReverseShell : bool;
global Reconnaissance : bool;
global ExecuteArbitraryCode : bool;
global AccesstoSensitiveFiles : bool;
global AccesstoMySQL : bool;

// initial attributes
global CGIscriptsenabled : bool init true;
global SOCKS5ProxyActive : bool init true;
global MisconfiguredApache : bool init true;
global MySQLServerVersion : bool init true;
global ApacheHTTPServerVersion : bool init true;

// goal
global DataExfiltration : bool;

module attacker

	getLoginData : bool;
	bribe : bool;
	getFiles : bool;
	bufferOverflow : bool;
	pathTraversal : bool;
	webRecon : bool;
	exfiltrateData : bool;
	searchOffice : bool;

	// actions
	//[noOp1] !DataExfiltration -> true;

	[exfiltrateData] sched=1 & !DataExfiltration & !DataExfiltration & !exfiltrateData & (AccesstoMySQL | ExecuteArbitraryCode) -> (DataExfiltration'=true) & (exfiltrateData'=true) & (sched'=2);
	[searchOffice] sched=1 & !DataExfiltration & !AccesstoMySQL & !searchOffice -> (AccesstoMySQL'=true) & (searchOffice'=true) & (sched'=2);
	[bribe] sched=1 & !DataExfiltration & !AccesstoMySQL & !bribe -> (AccesstoMySQL'=true) & (bribe'=true) & (sched'=2);
	[getLoginData] sched=1 & !DataExfiltration & !AccesstoMySQL & !getLoginData & (AccesstoSensitiveFiles) -> (AccesstoMySQL'=true) & (getLoginData'=true) & (sched'=2);
	[bufferOverflow] sched=1  & !DataExfiltration  & !ExecuteArbitraryCode & !bufferOverflow & (SOCKS5ProxyActive & AccesstoReverseShell & MySQLServerVersion) -> (ExecuteArbitraryCode'=true) & (bufferOverflow'=true) & (sched'=2);
	[getFiles] sched=1 & !DataExfiltration & !AccesstoSensitiveFiles & !getFiles & (MisconfiguredApache & AccesstoReverseShell) -> (AccesstoSensitiveFiles'=true) & (getFiles'=true) & (sched'=2);
	[pathTraversal] sched=1 & !DataExfiltration & !AccesstoReverseShell & !pathTraversal & (CGIscriptsenabled & ApacheHTTPServerVersion & Reconnaissance) -> (AccesstoReverseShell'=true) & (pathTraversal'=true) & (sched'=2);
	[webRecon] sched=1 & !DataExfiltration & !Reconnaissance & !webRecon -> (Reconnaissance'=true) & (webRecon'=true) & (sched'=2);
	
endmodule

label "terminate" =  DataExfiltration=true;

// reward structure based on the cost of the action
rewards "attacker"
	//[noOp1] true : 1;

    [exfiltrateData] true : 50; // goal
	[bufferOverflow] true : 30;
	[getLoginData] true : 10;
	[bribe] true : 500;
	[searchOffice] true : 500;
	[pathTraversal] true : 20;
	[getFiles] true : 30;
	[webRecon] true : 5;

endrewards

module defender

	NewMySQLVersion : bool;
	UpdateApache : bool;
	InstalledNewMySQL : bool;
	BackupMySQL : bool;
	UpdateMySQL : bool;
	BackupApache : bool;
	InstalledNewApache : bool;
	NewApacheVersion : bool;

	[changeCredentials] sched=2 & !DataExfiltration & AccesstoMySQL -> (AccesstoMySQL'=false) & (sched'=1);
	[protectFiles] sched=2 & !DataExfiltration & AccesstoSensitiveFiles -> (AccesstoSensitiveFiles'=false) & (sched'=1);
	[deactivateDOCKS5Proxy] sched=2 & !DataExfiltration & SOCKS5ProxyActive -> (SOCKS5ProxyActive'=false) & (sched'=1);
	[updateMySQL] sched=2 & !DataExfiltration & MySQLServerVersion & (UpdateMySQL) -> (MySQLServerVersion'=false) & (sched'=1);
	[reconfigureApache] sched=2 & !DataExfiltration & MisconfiguredApache -> (MisconfiguredApache'=false) & (sched'=1);
	[disableCGIscripts] sched=2 & !DataExfiltration & CGIscriptsenabled -> (CGIscriptsenabled'=false) & (sched'=1);
	[updateApache] sched=2 & !DataExfiltration & ApacheHTTPServerVersion & (UpdateApache) -> (ApacheHTTPServerVersion'=false) & (sched'=1);

	[rebootMySQL] sched=2 & !DataExfiltration & !UpdateMySQL & (BackupMySQL & InstalledNewMySQL) -> (UpdateMySQL'=true) & (sched'=1);
	[rebootApache] sched=2 & !DataExfiltration & !UpdateApache & (BackupApache & InstalledNewApache) -> (UpdateApache'=true) & (sched'=1);
	[backupMySQL] sched=2 & !DataExfiltration & !BackupMySQL -> (BackupMySQL'=true) & (sched'=1);
	[installMySQL] sched=2 & !DataExfiltration & !InstalledNewMySQL & (NewMySQLVersion) -> (InstalledNewMySQL'=true) & (sched'=1);
	[backupApache] sched=2 & !DataExfiltration & !BackupApache -> (BackupApache'=true) & (sched'=1);
	[installApache] sched=2 & !DataExfiltration & !InstalledNewApache & (NewApacheVersion) -> (InstalledNewApache'=true) & (sched'=1);
	[checkMySQLUpdates] sched=2 & !DataExfiltration & !NewMySQLVersion -> (NewMySQLVersion'=true) & (sched'=1);
	[checkApacheUpdates] sched=2 & !DataExfiltration & !NewApacheVersion -> (NewApacheVersion'=true) & (sched'=1); 

endmodule

// reward structure based on the cost of the action
rewards "defender"
	//[noOp2] true : 1;
	[exfiltrateData] true : 500;
	[changeCredentials] true : 70;
	[deactivateDOCKS5Proxy] true : 50;
	[protectFiles] true : 25;
	[disableCGIscripts] true : 50;
	[reconfigureApache] true : 30;

	[checkMySQLUpdates] true : 5;
	[backupMySQL] true : 50;
	[installMySQL] true : 20;
	[rebootMySQL] true : 30;
	[updateMySQL] true : 10;

	[checkApacheUpdates] true : 5;
	[backupApache] true : 50;
	[installApache] true : 20;
	[rebootApache] true : 30;
	[updateApache] true : 10;
endrewards

rewards "turns"
	true : 1;
endrewards