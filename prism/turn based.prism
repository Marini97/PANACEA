// Turn-based stochastic multi-player games
smg

// attacker and defender actors
player attacker
	attacker, 
	[bribe], [exfiltrateData], [bufferOverflow], [getLoginData], [searchOffice], [pathTraversal], [getFiles], [webRecon]
endplayer
player defender
	defender,
	[changeCredentials], [deactivateDOCKS5Proxy], [protectFiles], [disableCGIscripts], [reconfigureApache],	[checkMySQLUpdates], [backupMySQL],
	[installMySQL], 	[rebootMySQL], [updateMySQL], [checkApacheUpdates], [backupApache], [installApache], [rebootApache], [updateApache]
endplayer

global sched : [1..2] init 1;

// global attributes accessible from both players
global AccesstoReverseShell : bool;
global Reconnaissance : bool;
global ExecuteArbitraryCode : bool;
global AccesstoSensitiveFiles : bool;
global AccesstoMySQL : bool;

// initial attributes
global CGIscriptsenabled : bool init true;
global SOCKS5ProxyActive : bool init true;
global MisconfiguredApache : bool init true;
global MySQLServerVersion : bool init true;
global ApacheHTTPServerVersion : bool init true;

// goal
global DataExfiltration : bool;

module attacker

	bribe : bool;
	bufferOverflow : bool;
	getLoginData : bool;
	searchOffice : bool;
	pathTraversal : bool;
	getFiles : bool;
	webRecon : bool;

	// actions
	//[noOp1] !DataExfiltration -> true;

	[exfiltrateData] sched=1 & (ExecuteArbitraryCode | AccesstoMySQL) & !DataExfiltration -> (DataExfiltration'=true) & (sched'=2);
	[bufferOverflow] sched=1 & AccesstoReverseShell & SOCKS5ProxyActive & MySQLServerVersion  & !ExecuteArbitraryCode & !DataExfiltration & !bufferOverflow -> (ExecuteArbitraryCode'=true) & (bufferOverflow'=true) & (sched'=2);
	[getLoginData] sched=1 & AccesstoSensitiveFiles & !AccesstoMySQL & !DataExfiltration & !getLoginData -> (AccesstoMySQL'=true) & (getLoginData'=true) & (sched'=2);
	[bribe] sched=1 & !AccesstoMySQL & !DataExfiltration & !bribe -> (AccesstoMySQL'=true) & (bribe'=true) & (sched'=2);
	[searchOffice] sched=1 & !AccesstoMySQL & !DataExfiltration & !searchOffice -> (AccesstoMySQL'=true) & (searchOffice'=true) & (sched'=2);
	[pathTraversal] sched=1 & Reconnaissance & CGIscriptsenabled & ApacheHTTPServerVersion & !AccesstoReverseShell & !DataExfiltration & !pathTraversal -> (AccesstoReverseShell'=true) & (pathTraversal'=true) & (sched'=2);
	[getFiles] sched=1 & AccesstoReverseShell & !AccesstoSensitiveFiles & !DataExfiltration & !getFiles  -> (AccesstoSensitiveFiles'=true) & (getFiles'=true) & (sched'=2);
	[webRecon] sched=1 & !Reconnaissance & !DataExfiltration & !webRecon -> (Reconnaissance'=true) & (webRecon'=true) & (sched'=2);
	
endmodule

label "terminate" =  DataExfiltration=true;

// reward structure based on the cost of the action
rewards "attacker"
	//[noOp1] true : 1;

        [exfiltrateData] true : 0; // goal
	[bufferOverflow] true : 15;
	[getLoginData] true : 10;
	[bribe] true : 500;
	[searchOffice] true : 500;
	[pathTraversal] true : 20;
	[getFiles] true : 30;
	[webRecon] true : 5;
endrewards

module defender
	// local attributes
	NewMySQLVersion : bool;
	UpdateApache : bool;
	InstalledNewMySQL : bool;
	BackupMySQL : bool;
	UpdateMySQL : bool;
	BackupApache : bool;
	InstalledNewApache : bool;
	NewApacheVersion : bool;

	// actions
	//[noOp2] !DataExfiltration -> true;

	[changeCredentials] sched=2 & AccesstoMySQL & !DataExfiltration -> (AccesstoMySQL'=false) & (sched'=1);
	[deactivateDOCKS5Proxy] sched=2 & SOCKS5ProxyActive & !DataExfiltration -> (SOCKS5ProxyActive'=false) & (sched'=1);
	[protectFiles] sched=2 & AccesstoSensitiveFiles & !DataExfiltration -> (AccesstoSensitiveFiles'=false) & (sched'=1);
	[disableCGIscripts] sched=2 & CGIscriptsenabled & !DataExfiltration -> (CGIscriptsenabled'=false) & (sched'=1);
	[reconfigureApache] sched=2 & MisconfiguredApache & !DataExfiltration -> (MisconfiguredApache'=false) & (sched'=1);

	[checkMySQLUpdates] sched=2 & !NewMySQLVersion & !DataExfiltration-> (NewMySQLVersion'=true) & (sched'=1);
	[backupMySQL] sched=2 & !BackupMySQL & !DataExfiltration -> (BackupMySQL'=true) & (sched'=1);
	[installMySQL] sched=2 & NewMySQLVersion & !InstalledNewMySQL & !DataExfiltration ->  (InstalledNewMySQL'=true) & (sched'=1);
	[rebootMySQL] sched=2 & InstalledNewMySQL & BackupMySQL & !UpdateMySQL & !DataExfiltration  -> (UpdateMySQL'=true) & (sched'=1);
	[updateMySQL] sched=2 & UpdateMySQL & MySQLServerVersion & !DataExfiltration -> (MySQLServerVersion'=false) & (sched'=1);

	[checkApacheUpdates] sched=2 & !NewApacheVersion & !DataExfiltration -> (NewApacheVersion'=true) & (sched'=1); 
	[backupApache] sched=2 & !BackupApache & !DataExfiltration -> (BackupApache'=true) & (sched'=1);
	[installApache] sched=2 & NewApacheVersion & !InstalledNewApache & !DataExfiltration -> (InstalledNewApache'=true) & (sched'=1);
	[rebootApache] sched=2 & InstalledNewApache & BackupApache & !UpdateApache & !DataExfiltration -> (UpdateApache'=true) & (sched'=1);
	[updateApache] sched=2 & UpdateApache & ApacheHTTPServerVersion & !DataExfiltration -> (ApacheHTTPServerVersion'=false) & (sched'=1);


endmodule

// reward structure based on the cost of the action
rewards "defender"
	//[noOp2] true : 1;
	[exfiltrateData] true : 500;
	[changeCredentials] true : 70;
	[deactivateDOCKS5Proxy] true : 50;
	[protectFiles] true : 25;
	[disableCGIscripts] true : 50;
	[reconfigureApache] true : 30;

	[checkMySQLUpdates] true : 5;
	[backupMySQL] true : 50;
	[installMySQL] true : 20;
	[rebootMySQL] true : 30;
	[updateMySQL] true : 10;

	[checkApacheUpdates] true : 5;
	[backupApache] true : 50;
	[installApache] true : 20;
	[rebootApache] true : 30;
	[updateApache] true : 10;
endrewards

rewards "turns"
	true : 1;
endrewards